#version 330 core

in vec2 retTexCoords;
out vec4 FragColor;

uniform sampler2D screenTexture;
uniform int effectType;

const float offset = 1.0 / 300.0;

void main() {
    vec3 col = vec3(texture(screenTexture, retTexCoords).rgb);

    if (effectType == 1) {
        float gray = dot(col, vec3(0.299, 0.587, 0.114));
        col = vec3(gray);
    } else if (effectType == 2) {
        col = vec3(1.0) - col;
    } else if (effectType == 3) {
        vec2 offsets[9] = vec2[](
            vec2(-offset,  offset), vec2(0.0f,    offset), vec2(offset,  offset),
            vec2(-offset,  0.0f),   vec2(0.0f,    0.0f),   vec2(offset,  0.0f),
            vec2(-offset, -offset), vec2(0.0f,   -offset), vec2(offset, -offset)
        );

        float kernel[9] = float[](
            1, 1, 1,
            1, -8, 1,
            1, 1, 1
        );

        vec3 sampleTex[9];
        for (int i = 0; i < 9; i++) {
            sampleTex[i] = vec3(texture(screenTexture, retTexCoords.st + offsets[i]));
        }

        vec3 edgeColor = vec3(0.0);
        for (int i = 0; i < 9; i++) {
            edgeColor += sampleTex[i] * kernel[i];
        }
        col = edgeColor;
    } else if (effectType == 4) {
        col = abs(col - vec3(0.5)) + 0.5;
    }

    FragColor = vec4(col, 1.0);
}
