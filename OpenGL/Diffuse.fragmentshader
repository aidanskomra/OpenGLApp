#version 330

in vec3 retFragPos;			// Fragment position
in vec3 retNormal;			// Fragment normal
in vec2 retTexCoord;		// Fragment texture coordinates
in vec3 retViewDirection;	// Fragment View Direction
in mat3 retTBN;             // Output the Tangent Bitangent Normal

out vec4 FragColor;

struct Material
{
	float specularStrength;
	sampler2D diffuseTexture;
	sampler2D specularTexture;
	sampler2D normalTexture;
};

struct Light {
    vec3 position;
    vec3 color;
    vec3 ambientColor;
    vec3 diffuseColor;
    vec3 specularColor;
};


uniform Material material;
#define NUM_LIGHTS 1
uniform Light light[NUM_LIGHTS];
uniform bool EnableNormalMaps = false;

void main()
{
	vec4 diffColor = texture(material.diffuseTexture, retTexCoord);
	if (diffColor.a == 0)
	{
		discard;
	}

	vec4 finalColor = vec4(0);
	for (int i = 0; i < NUM_LIGHTS; i++)
	{
	// Calculate lambertian strength
	vec3 lightDir = normalize(light[i].position - retFragPos);
	vec3 normal = retNormal;
	if (EnableNormalMaps == true)
	{
		normal = texture(material.normalTexture, retTexCoord).rgb;
		normal = normal * 2.0 - 1.0;
		normal = normalize(retTBN * normal);
	}
	float lambertianStrength = dot(lightDir, normal);

	// Calculate specular strength
	vec3 refl = reflect(-lightDir, normal);
	float specularStrength = pow(max(dot(refl, retViewDirection), 0.0f), material.specularStrength);
	

	vec3 ambient = diffColor.rgb * light[i].ambientColor / NUM_LIGHTS;
	vec3 lambertian = lambertianStrength * diffColor.rgb * light[i].diffuseColor;
	vec3 specular = specularStrength * texture(material.specularTexture, retTexCoord).rgb * light[i].specularColor;
	finalColor += vec4(ambient + lambertian + specular, 1.0f);
	
	}

	FragColor = vec4(finalColor.rgb, diffColor.a);
}